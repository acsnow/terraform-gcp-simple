import "tfplan/v2" as tfplan

allComputeInstances = filter tfplan.resource_changes as _, resource_changes {
	resource_changes.type is "google_compute_instance" and
		resource_changes.mode is "managed" and
		(resource_changes.change.actions contains "create" or
			resource_changes.change.actions is ["update"])
}

allComputeInstanceTemplates = filter tfplan.resource_changes as _, resource_changes {
	resource_changes.type is "google_compute_instance_template" and
		resource_changes.mode is "managed" and
		(resource_changes.change.actions contains "create" or
			resource_changes.change.actions is ["update"])
}

can_ip_forward_is_disabled_in_compute_instance = rule {
	all allComputeInstances as _, instance {
		instance.change.after.can_ip_forward is false
	}
}

can_ip_forward_is_disabled_in_compute_instance_template = rule {
	all allComputeInstanceTemplates as _, template {
		template.change.after.can_ip_forward is false
	}
}

// ---------------------------------------------------------
// Category:    Infrastructure (IaaS)
// Provider:    hashicorp/google
// Resource:    google_compute_instance
//              google_compute_instance_template
// Check:       can_ip_forward is false
// ---------------------------------------------------------
// Ensure that IP forwarding is not enabled on Instance.
// ---------------------------------------------------------
main = rule {
	can_ip_forward_is_disabled_in_compute_instance and
	can_ip_forward_is_disabled_in_compute_instance_template
}
